plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.bladecoder.engine'

// java
sourceCompatibility=1.8
targetCompatibility=1.8

[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

sourceSets {
	main {
		java.srcDirs = ['src']
		resources.srcDirs = ['resources']
	}
}


jar {
	manifest.attributes += [
			'github': 'https://github.com/bladecoder/bladecoder-adventure-engine/',
			'license': 'Apache-2.0',
			'group': project.group,
			'version': project.version,
			'libgdx': libgdxVersion,
			'java': targetCompatibility,
			'timestamp': System.currentTimeMillis()
	]
}

dependencies {
  api "com.badlogicgames.gdx:gdx:$libgdxVersion"
  implementation "com.badlogicgames.gdx-controllers:gdx-controllers-core:$gdxControllersVersion"
  implementation "com.badlogicgames.gdx:gdx-freetype:$libgdxVersion"
  implementation "com.bladecoder.ink:blade-ink:$bladeInkVersion"
  api fileTree(dir: 'libs', include: '*.jar')
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'blade-engine'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Blade Engine'
                description = 'Classic point and click adventure game engine'
                url = 'https://github.com/bladecoder/bladecoder-adventure-engine'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'bladecoder'
                        name = 'Rafael Garcia'
                        email = 'bladecoder@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:bladecoder/bladecoder-adventure-engine.git'
                    developerConnection = 'scm:git@github.com:bladecoder/bladecoder-adventure-engine.git'
                    url = 'scm:git@github.com:bladecoder/bladecoder-adventure-engine.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username findProperty("sonatypeUsername")
                password findProperty("sonatypePassword")
            }
        }
    }
}

signing {
    if(!version.endsWith('SNAPSHOT')) {
        sign publishing.publications.mavenJava
    }
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}