plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
    id "com.diffplug.spotless" version "7.2.1"
}

group = 'com.bladecoder.engine'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

jar {
	manifest.attributes += [
			'github': 'https://github.com/bladecoder/bladecoder-adventure-engine/',
			'license': 'Apache-2.0, Spine Runtimes Software License',
			'group': project.group,
			'version': project.version,
			'libgdx': libgdxVersion,
			'java': java.targetCompatibility,
			'timestamp': System.currentTimeMillis()
	]
}

dependencies {
  implementation "com.badlogicgames.gdx:gdx:$libgdxVersion"
  implementation "com.esotericsoftware.spine:spine-libgdx:4.2.7"
  
  implementation project(":blade-engine")
}


tasks.register('sourcesJar', Jar) {
    from sourceSets.main.allJava
    archiveClassifier = 'sources'
}

tasks.register('javadocJar', Jar) {
    from javadoc
    archiveClassifier = 'javadoc'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'blade-engine-spine-plugin'
            from components.java
            artifact sourcesJar
            artifact javadocJar

            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'Blade Engine Spine Plugin'
                description = 'Classic point and click adventure game engine - Spine plugin'
                url = 'https://github.com/bladecoder/bladecoder-adventure-engine'

                licenses {
                    license {
                        name='Spine Runtimes Software License'
                        url='http://esotericsoftware.com/files/license.txt'
                    }
                }
                developers {
                    developer {
                        id = 'bladecoder'
                        name = 'Rafael Garcia'
                        email = 'bladecoder@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:bladecoder/bladecoder-adventure-engine.git'
                    developerConnection = 'scm:git@github.com:bladecoder/bladecoder-adventure-engine.git'
                    url = 'scm:git@github.com:bladecoder/bladecoder-adventure-engine.git'
                }
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://ossrh-staging-api.central.sonatype.com/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username project.hasProperty('NEXUS_USERNAME') ? NEXUS_USERNAME : "$System.env.NEXUS_USERNAME"
                password project.hasProperty('NEXUS_PASSWORD') ? NEXUS_PASSWORD : "$System.env.NEXUS_PASSWORD"
            }
        }
    }
}

signing {
    if(!version.endsWith('SNAPSHOT')) {
        useGpgCmd()
        sign publishing.publications.mavenJava
    }
}


javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}
