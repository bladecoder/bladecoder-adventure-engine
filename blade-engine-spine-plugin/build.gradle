apply plugin: "java"
apply plugin: 'java-library'
apply plugin: "signing"
apply plugin: "maven"

group = 'com.bladecoder.engine'

// java
 sourceCompatibility = 1.7
 targetCompatibility=1.7
 [compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

jar {
	manifest.attributes += [
			'github': 'https://github.com/bladecoder/bladecoder-adventure-engine/',
			'license': 'Apache-2.0, Spine Runtimes Software License',
			'group': project.group,
			'version': project.version,
			'libgdx': libgdxVersion,
			'java': targetCompatibility,
			'timestamp': System.currentTimeMillis()
	]
}

    javadoc {
	title = "Bladecoder Adventure Engine Spine Plugin"
	options {
		memberLevel = JavadocMemberLevel.PUBLIC
		author true
		setUse true
    		encoding "UTF-8"
	}
    }

dependencies {
  api "com.badlogicgames.gdx:gdx:$libgdxVersion"
  api "com.esotericsoftware.spine:spine-libgdx:3.8.55.1"
  
  implementation project(":blade-engine")
}


task enginedocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from 'build/docs/javadoc'
}


task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}

artifacts {
    //archives jar
    archives enginedocJar
    archives sourcesJar
}

//********* artifact signing *********
if(project.hasProperty("release")) {
    signing {  
        sign configurations.archives
    }
} else {
    task signArchives {
        // do nothing
    }
}

uploadArchives {
    repositories {
        if (!project.hasProperty("ci") && !project.hasProperty("release")) {
            mavenLocal()
        } else {
            mavenDeployer {
                if(project.hasProperty("release")) {
                    beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
                }

                repository(url: sonatypeRepositoryUrl) {                 
                  authentication(
                    userName: sonatypeUsername, 
                  	password: sonatypePassword
                  )
                }

                pom.project {
                   name 'blade-engine-spine-plugin'
                   packaging 'jar'
                   description 'Classic point and click adventure game engine - Spine plugin'
                   url 'https://github.com/bladecoder/bladecoder-adventure-engine'

                   scm {
                       url 'scm:git@github.com:bladecoder/bladecoder-adventure-engine.git'
                       connection 'scm:git@github.com:bladecoder/bladecoder-adventure-engine.git'
                       developerConnection 'scm:git@github.com:bladecoder/bladecoder-adventure-engine.git'
                   }

                   licenses {
                       license {
                           name 'The Apache Software License, Version 2.0'
                           url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                           distribution 'repo'
                       }
                       
                       license {
                           name 'Spine Runtimes Software License'
                           url 'http://esotericsoftware.com/files/license.txt'
                           distribution 'repo'
                       }
                   }

                   developers {
                       developer {
                           id 'rgarcia'
                           name 'Rafael Garcia'
                       } 
                   }
               }
            }
        }
    }
}
