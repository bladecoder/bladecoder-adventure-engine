apply plugin: 'java-library'
apply plugin: 'application'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets.main.resources.srcDirs += [rootProject.file('assets').absolutePath]
mainClassName = "com.bladecoder.engine.DesktopLauncher"
applicationDefaultJvmArgs = ["-XstartOnFirstThread"]

dependencies {
    implementation project(":core")
    implementation "com.badlogicgames.gdx:gdx-backend-lwjgl3:$gdxVersion"
    implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    implementation "com.badlogicgames.gdx:gdx-lwjgl3-angle:$gdxVersion"
    implementation "com.badlogicgames.gdx-controllers:gdx-controllers-desktop:$gdxControllersVersion"
}

run {
    standardInput = System.in
    ignoreExitValue = true

    if (project.hasProperty("appArgs")) {
        args Eval.me(appArgs)
    }
}


task dist(type: Jar) {
    duplicatesStrategy(DuplicatesStrategy.EXCLUDE)
    manifest {
        attributes 'Main-Class': project.mainClassName
    }
    dependsOn configurations.runtimeClasspath
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
    with jar
}

dist.dependsOn classes
dist.dependsOn ':core:jar'
