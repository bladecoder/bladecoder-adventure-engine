buildscript {
  repositories {
    mavenCentral()
  }
  
  dependencies {
    classpath "com.badlogicgames.gdx:gdx-tools:$libgdxVersion"
  }
}

apply plugin: 'java-library'
apply plugin: "java"
apply plugin: 'application'

group = 'com.bladecoder.engineeditor'
mainClassName = 'com.bladecoder.engineeditor.Main'

// java
sourceCompatibility=1.7
targetCompatibility=1.7
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'

jar {
	manifest.attributes += [
			'github': 'https://github.com/bladecoder/bladecoder-adventure-engine/',
			'license': 'Apache-2.0',
			'group': project.group,
			'version': project.version,
			'libgdx': libgdxVersion,
			'java': targetCompatibility,
			'timestamp': System.currentTimeMillis()
	]
}

javadoc {
	title = "Adventure Editor"
	options {
		memberLevel = JavadocMemberLevel.PUBLIC
		author true
		setUse true
    	encoding "UTF-8"
	}
}

dependencies {
  api "com.badlogicgames.gdx:gdx:$libgdxVersion"
  api "com.badlogicgames.gdx:gdx-freetype:$libgdxVersion"
  api "com.badlogicgames.gdx:gdx-tools:$libgdxVersion"
  api fileTree(dir: 'libs', include: '*.jar')
  api "com.badlogicgames.gdx:gdx-backend-lwjgl:$libgdxVersion"
  api "com.badlogicgames.gdx:gdx-platform:$libgdxVersion:natives-desktop"
  api "com.badlogicgames.gdx:gdx-freetype-platform:$libgdxVersion:natives-desktop"
  api "commons-io:commons-io:2.1"
  api "com.bladecoder.packr:packr:2.1"
  api "eu.infomas:annotation-detector:3.0.5"
  api "com.strongjoshua:libgdx-inGameConsole:0.9.0"
  api "com.kotcrab.vis:vis-ui:1.4.2-SNAPSHOT"
  api "com.bladecoder.ink:blade-ink:$bladeInkVersion"

  implementation project(":blade-engine")
  implementation project(":blade-engine-spine-plugin")
}

task setVersion(type: WriteProperties) {

	def props = new Properties()
	def propFile = project.file("./src/main/resources/versions.properties");
	props.load(new FileReader(propFile))

	props."version" = version
	props."libgdxVersion" = libgdxVersion
	props."roboVMVersion" = roboVMVersion
	props."roboVMGradlePluginVersion" = roboVMGradlePluginVersion
	props."buildToolsVersion" = buildToolsVersion
	props."androidAPILevel" = androidAPILevel
	props."androidGradlePluginVersion" = androidGradlePluginVersion
	props."bladeInkVersion" = bladeInkVersion

	setProperties(props);
	setOutputFile(propFile);
	
	doLast {
		println "Set version info in versions.properties"
	}
}

processResources.finalizedBy(setVersion)

// Run `gradle pack` task to generate editor atlases
import com.badlogic.gdx.tools.texturepacker.TexturePacker
task pack {

	doLast {
	  TexturePacker.process(
	    'adventure-editor/assets-raw/editor/BladeSkin',           // Raw assets path.
	    'adventure-editor/src/main/resources/skin/BladeSkin',     // Output directory.
	    'BladeSkin-ldpi'              			 				  // Name of the generated atlas (without extension).
	  )
	  
	  TexturePacker.process(
	    'adventure-editor/assets-raw/editor/icons',
	    'adventure-editor/src/main/resources/images', 
	    'icons'  
	  )
	  
	  TexturePacker.process(
	    'adventure-editor/assets-raw/engine/ui-atlas', 
	    'adventure-editor/src/main/resources/projectTmpl/assets/ui/1', 
	    'ui'
	  )
  }
}

// include flatpack files in .zip
distZip {
  into(project.name + '-' + project.version) {
      from '.'
      include 'desktop/*'
  }
}
